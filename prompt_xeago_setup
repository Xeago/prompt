prompt_xeago_setup () {
  export VIRTUAL_ENV_DISABLE_PROMPT=1 # disallow virtualenv from updating prompt

  PROMPT=''
  if [[ -n $SSH_CONNECTION || $UID -eq 0 ]]; then
      PROMPT+='%F{white}%n%F{magenta}@%F{yellow}%m%f:'
  fi
  PROMPT+='%F{cyan}${${(%):-%~}//\//%F{blue\}/%F{cyan\}}%f'
  PROMPT+='%(!.%{%F{magenta}%}#.%{%F{red}%}$)%f '
  
  prompt_opts=(cr subst percent)
  
  autoload -Uz vcs_info
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:git*' formats "%f %b"
  zstyle ':vcs_info:git*' actionformats "%F{red}%a%f %b"
  
  PR_GIT_UPDATE=1
  function zsh_git_prompt_precmd {
    if [[ -n "$PR_GIT_UPDATE" ]] ; then
      vcs_info
      PR_GIT_UPDATE=
    fi
  }
  precmd_functions+=('zsh_git_prompt_precmd')
  
  function zsh_git_prompt_chpwd {
    PR_GIT_UPDATE=1
  }
  chpwd_functions+=('zsh_git_prompt_chpwd')
  
  function zsh_git_prompt_preexec {
    case "$(history $HISTCMD)" in 
      *git*)
        PR_GIT_UPDATE=1
        ;;
    esac
  }
  preexec_functions+=('zsh_git_prompt_preexec')
  
  RPROMPT='${vcs_info_msg_0_} ${VIRTUAL_ENV:t}'
}

prompt_xeago_setup "$@"
